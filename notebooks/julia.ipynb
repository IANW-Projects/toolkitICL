{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Example using `julia`\n",
    "\n",
    "This notebook demonstrates the usage of `toolkitICL` using `julia` to write the configuration file."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# HDF5 is used for the configuration file.\n",
    "using HDF5\n",
    "\n",
    "# helper functions to convert a juia type to its OpenCL equivalent\n",
    "type_to_name(::Type{Int8}) = \"char\"\n",
    "type_to_name(::Type{UInt8}) = \"uchar\"\n",
    "type_to_name(::Type{Int16}) = \"short\"\n",
    "type_to_name(::Type{UInt16}) = \"ushort\"\n",
    "type_to_name(::Type{Int32}) = \"int\"\n",
    "type_to_name(::Type{UInt32}) = \"uint\"\n",
    "type_to_name(::Type{Int64}) = \"long\"\n",
    "type_to_name(::Type{UInt64}) = \"ulong\"\n",
    "type_to_name(::Type{Float32}) = \"float\"\n",
    "type_to_name(::Type{Float64}) = \"double\"\n",
    "\n",
    "# Define an OpenCL kernel and write it to a file. Of course, you can\n",
    "# write the kernel file in an editor for more complex use cases.\n",
    "copy_kernel =\n",
    "\"\"\"\n",
    "#ifdef cl_khr_fp64\n",
    "  #pragma OPENCL EXTENSION cl_khr_fp64 : enable\n",
    "#else\n",
    "  #error \\\"IEEE-754 double precision not supported by OpenCL implementation.\\\"\n",
    "#endif\n",
    "\n",
    "kernel void copy(global COPYTYPE const* in, global COPYTYPE* out)\n",
    "{\n",
    "  const int gid = get_global_id(0);\n",
    "  out[gid] = in[gid];\n",
    "}\n",
    "\"\"\"\n",
    "kernel_url = \"copy_kernel_jl.cl\"\n",
    "open(kernel_url, \"w\") do io\n",
    "    write(io, copy_kernel)\n",
    "end\n",
    "\n",
    "# input parameters\n",
    "const LENGTH = 32\n",
    "T = Float64\n",
    "\n",
    "# generate input/output arrays for OpenCL\n",
    "input = Vector{T}(0:(LENGTH-1))\n",
    "output = fill(zero(T), LENGTH)\n",
    "\n",
    "# write the configuration file\n",
    "filename = \"copy_\" * type_to_name(T) * \"_test_jl.h5\"\n",
    "h5open(filename, \"w\") do io\n",
    "    # compiler arguments for OpenCL\n",
    "    write(io, \"settings/kernel_settings\", \"-DCOPYTYPE=\"*type_to_name(T))\n",
    "    \n",
    "    # URL of the file containing the kernels\n",
    "    write(io, \"kernel_url\", kernel_url)\n",
    "    # Instead, you could also write(io, \"Kernel_Source\", copy_kernel).\n",
    "    \n",
    "    # kernels to be executed\n",
    "    write(io, \"kernels\", [\"copy\"])\n",
    "\n",
    "    # OpenCL ranges\n",
    "    tmp_range = Int32[LENGTH, 1, 1]\n",
    "    write(io, \"settings/global_range\", tmp_range)\n",
    "\n",
    "    tmp_range .= 0\n",
    "    write(io, \"settings/local_range\", tmp_range)\n",
    "    write(io, \"settings/range_start\", tmp_range)\n",
    "\n",
    "    # data\n",
    "    write(io, \"data/in\", input)\n",
    "    write(io, \"data/out\", output)\n",
    "end\n",
    "\n",
    "\n",
    "# call toolkitICL\n",
    "run(`toolkitICL -c $filename`)\n",
    "\n",
    "\n",
    "# check results\n",
    "out_filename = \"out_\" * filename\n",
    "in_test = h5read(out_filename, \"data/in\")\n",
    "vec(in_test) == input || println(\"Wrong result for 'in'\")\n",
    "\n",
    "out_test = h5read(out_filename, \"data/out\")\n",
    "vec(out_test) == input || println(\"Wrong result for 'out'\")\n",
    "\n",
    "println(\"Finished successfully.\")"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# remove temporary files\n",
    "rm_if_exists(s) = isfile(s) && rm(s)\n",
    "\n",
    "rm_if_exists(filename)\n",
    "rm_if_exists(kernel_url)\n",
    "rm_if_exists(out_filename)\n",
    "readdir()"
   ]
  }
 ],
 "metadata": {
  "@webio": {
   "lastCommId": "A27B0180D1C24894828C284A7FB8DFD3",
   "lastKernelId": "7cd66453-71a4-49d1-90c3-d5d41f530582"
  },
  "kernelspec": {
   "display_name": "Julia 1.1.1-pre",
   "language": "julia",
   "name": "julia-1.1"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.1.1"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
